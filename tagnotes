#!/usr/bin/env perl
use Mojolicious::Lite;

use FindBin '$Bin';
use lib "$Bin/lib";
use TagNotes;

plugin 'Config';

# make util functions accessible as helpers
plugin 'TagNotes::Util';

# configure tagnotes helper
my $tagnotes = TagNotes->new(notes_dir => app->config->{notes_dir});
app->helper(tagnotes => sub {$tagnotes});

get '/' => sub {
    my $c = shift;
    my @notes = sort {$a->raw cmp $b->raw} @{$c->tagnotes->get_all_notes};
    $c->stash(notes => \@notes);
} => 'list';

get '/note/:uuid' => sub {
    my $c = shift;

    # lookup
    my $note = $c->tagnotes->get_note($c->param('uuid'));
    return $c->reply->not_found unless defined $note;

    # sort
    my @notes = sort {$a->raw cmp $b->raw} @{$c->tagnotes->get_all_notes};
    $c->stash(note => $note, notes => \@notes);
} => 'note';

get '/tag/:tag' => sub {
    my $c = shift;

    # lookup
    my $tag     = $c->param('tag');
    my $notes   = $c->tagnotes->get_tag_notes($tag);
    return $c->reply->not_found unless @$notes;

    # render like note listing
    my @notes = sort {$a->raw cmp $b->raw} @$notes;
    $c->render(
        tag         => $tag,
        notes       => \@notes,
        template    => 'list',
    );
} => 'tag';

app->start;
