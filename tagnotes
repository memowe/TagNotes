#!/usr/bin/env perl
use Mojolicious::Lite;

use FindBin '$Bin';
use lib "$Bin/lib";
use TagNotes;
use List::Util 'first';

plugin 'Config';

# configure tagnotes helper
my $tagnotes = TagNotes->new(notes_dir => app->config->{notes_dir});
app->helper(tagnotes => sub {$tagnotes});

get '/' => sub {
    my $c = shift;
    $c->stash(notes => $c->tagnotes->notes);
} => 'home';

get '/note/:uuid' => sub {
    my $c = shift;

    # lookup
    my $uuid    = $c->param('uuid');
    my $notes   = $c->tagnotes->notes;
    my $note    = first {$_->path =~ /note_$uuid\.md$/} @$notes;
    return $c->reply->not_found unless defined $note;

    # prepare
    $c->stash(note => $note, notes => $notes);
} => 'note';

app->start;
